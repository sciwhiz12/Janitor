plugins {
    id 'application'
}
apply from: 'version.gradle'

group = "tk.sciwhiz12.janitor"
version = versionInfo["version"] ?: "unknown"
println("Version: $version")

repositories {
    mavenCentral()
}

dependencies {
    implementation "net.dv8tion:JDA:$jda_ver"
    implementation "com.google.guava:guava:$guava_ver"
    implementation "com.fasterxml.jackson.core:jackson-core:$jackson_ver"
    implementation "com.fasterxml.jackson.core:jackson-annotations:$jackson_ver"
    implementation "com.fasterxml.jackson.core:jackson-databind:$jackson_ver"
    implementation "ch.qos.logback:logback-classic:$logback_ver"
    
    testImplementation "org.junit.jupiter:junit-jupiter:$junit_ver"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

application {
    mainClass = 'tk.sciwhiz12.janitor.App'
}

jar {
    archivesBaseName = "janitor-bot"
    manifest {
        attributes([
                "Specification-Title"   : "Janitor",
                "Specification-Vendor"  : "sciwhiz12",
                "Specification-Version" : project.version,
                "Implementation-Title"  : "janitor-bot",
                "Implementation-Vendor" : "sciwhiz12",
                "Implementation-Version": project.version,
                "Commit-Hash"           : versionInfo["hash"] ?: "0000000000000000000000000000000000000000",
                "Commit-Timestamp"      : versionInfo["timestamp"] ?: "1980-01-01T00:00:00+00:00"
        ] as LinkedHashMap)
    }
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}
